<!--
    Copyright Â© 2024 AO Kaspersky Lab.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.
-->

<xsl:transform xmlns="http://schemas.microsoft.com/winfx/2006/xaml/presentation"
               xmlns:msxsl="urn:schemas-microsoft-com:xslt"
               xmlns:x="http://schemas.microsoft.com/winfx/2006/xaml"
               xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
               exclude-result-prefixes="msxsl"
               version="1.0">

	<xsl:output method="xml" indent="yes" encoding="UTF-8" omit-xml-declaration="yes"/>

	<xsl:param name="excludedControlsFilter"/>
	<xsl:param name="excludedControlsFilterDelimiter"/>
	<xsl:param name="externalResourceDictionaries"/>
	<xsl:param name="externalResourceDictionariesDelimiter"/>
	<xsl:param name="conditionalNamespacePrefix"/>
	<xsl:param name="commentText"/>

	<xsl:variable name="excludedControls" select="$excludedControlsFilter"/>
	<xsl:variable name="excludedControlsDelimiter" select="$excludedControlsFilterDelimiter"/>
	<xsl:variable name="comment" select="$commentText"/>

	<xsl:include href="BadgeStyle.xslt"/>
	<xsl:include href="BulletStyle.xslt"/>
	<xsl:include href="ButtonStyle.xslt"/>
	<xsl:include href="CarouselItemStyle.xslt"/>
	<xsl:include href="CheckBoxRadioButtonStyle.xslt"/>
	<xsl:include href="ChipsControlStyle.xslt"/>
	<xsl:include href="ChipsItemStyle.xslt"/>
	<xsl:include href="CodeInputStyle.xslt"/>
	<xsl:include href="ContentButtonStyle.xslt"/>
	<xsl:include href="ContextMenuStyle.xslt"/>
	<xsl:include href="DateTimeInputStyle.xslt"/>
	<xsl:include href="DateTimePopupFooterButtonStyle.xslt"/>
	<xsl:include href="DateTimePopupItemButtonStyle.xslt"/>
	<xsl:include href="DateTimePopupPresenterStyle.xslt"/>
	<xsl:include href="DividerStyle.xslt"/>
	<xsl:include href="HyperlinkStyle.xslt"/>
	<xsl:include href="IconButtonStyle.xslt"/>
	<xsl:include href="IconStyle.xslt"/>
	<xsl:include href="ImageGalleryButtonStyle.xslt"/>
	<xsl:include href="ImageGalleryStyle.xslt"/>
	<xsl:include href="ImageGalleryListButtonStyle.xslt"/>
	<xsl:include href="ImageGalleryListStyle.xslt"/>
	<xsl:include href="InteractivityDialogStyle.xslt"/>
	<xsl:include href="InteractivityNotificationStyle.xslt"/>
	<xsl:include href="InteractivityOverlayStyle.xslt"/>
	<xsl:include href="IslandButtonStyle.xslt"/>
	<xsl:include href="IslandStyle.xslt"/>
	<xsl:include href="IslandToggleButtonStyle.xslt"/>
	<xsl:include href="ListMenuItemStyle.xslt"/>
	<xsl:include href="MenuItemStyle.xslt"/>
	<xsl:include href="NavigationMenuButtonStyle.xslt"/>
	<xsl:include href="NavigationMenuFooterButtonStyle.xslt"/>
	<xsl:include href="NotificationHintStyle.xslt"/>
	<xsl:include href="NotificationPanelStyle.xslt"/>
	<xsl:include href="NotificationSpinnerStyle.xslt"/>
	<xsl:include href="NumberInputStyle.xslt"/>
	<xsl:include href="PasswordInputStyle.xslt"/>
	<xsl:include href="PopupStyle.xslt"/>
	<xsl:include href="ProgressBarStyle.xslt"/>
	<xsl:include href="RoundProgressStyle.xslt"/>
	<xsl:include href="RoundTimerStyle.xslt"/>
	<xsl:include href="ScrollBarStyle.xslt"/>
	<xsl:include href="SearchStyle.xslt"/>
	<xsl:include href="SelectableTextStyle.xslt"/>
	<xsl:include href="SelectItemStyle.xslt"/>
	<xsl:include href="SelectPresenterStyle.xslt"/>
	<xsl:include href="SelectStyle.xslt"/>
	<xsl:include href="SpinnerStyle.xslt"/>
	<xsl:include href="SplitButtonStyle.xslt"/>
	<xsl:include href="SsoButtonStyle.xslt"/>
	<xsl:include href="StatusBulletStyle.xslt"/>
	<xsl:include href="StatusTagStyle.xslt"/>
	<xsl:include href="SwitchStyle.xslt"/>
	<xsl:include href="TabMenuItemStyle.xslt"/>
	<xsl:include href="TabMenuStyle.xslt"/>
	<xsl:include href="TagStyle.xslt"/>
	<xsl:include href="TextAreaStyle.xslt"/>
	<xsl:include href="TextInputStyle.xslt"/>
	<xsl:include href="TextStyle.xslt"/>
	<xsl:include href="TextViewerStyle.xslt"/>
	<xsl:include href="ToolTipStyle.xslt"/>

	<xsl:template match="UiKit">
		<xsl:call-template name="getAutogeneratedComment">
			<xsl:with-param name="source" select="@Source"/>
			<xsl:with-param name="comment" select="$comment"/>
		</xsl:call-template>

		<xsl:call-template name="assertResourceDictionaries">
			<xsl:with-param name="externalResourceDictionaries" select="$externalResourceDictionaries"/>
			<xsl:with-param name="delimiter" select="$externalResourceDictionariesDelimiter"/>
		</xsl:call-template>

		<ResourceDictionary xmlns="http://schemas.microsoft.com/winfx/2006/xaml/presentation"
							xmlns:uikit="http://schemas.kaspirin.com/common/uikit"
							xmlns:visuals="http://schemas.kaspirin.com/common/visuals"
							xmlns:x="http://schemas.microsoft.com/winfx/2006/xaml">

			<!--Merge product resource dictionary with base styles containing control templates-->
			<ResourceDictionary.MergedDictionaries>
				<xsl:call-template name="generateResourceDictionaries">
					<xsl:with-param name="externalResourceDictionaries" select="$externalResourceDictionaries" />
					<xsl:with-param name="delimiter" select="$externalResourceDictionariesDelimiter" />
				</xsl:call-template>
			</ResourceDictionary.MergedDictionaries>

			<xsl:apply-templates select="Controls" />
		</ResourceDictionary>
	</xsl:template>

	<xsl:template name="assertResourceDictionaries">
		<xsl:param name="externalResourceDictionaries" />
		<xsl:param name="delimiter" />

		<xsl:if test="normalize-space($externalResourceDictionaries) = ''">
			<xsl:message terminate="yes">External resource dictionaries are not set.</xsl:message>
		</xsl:if>

		<xsl:if test="normalize-space($delimiter) = ''">
			<xsl:message terminate="yes">External resource dictionaries delimiter is not set.</xsl:message>
		</xsl:if>
	</xsl:template>

	<xsl:template name="generateResourceDictionaries">
		<xsl:param name="externalResourceDictionaries" />
		<xsl:param name="delimiter" />

		<xsl:variable name="resourceDictionaries">
			<xsl:call-template name="split">
				<xsl:with-param name="value" select="$externalResourceDictionaries"/>
				<xsl:with-param name="delimiter" select="$delimiter"/>
			</xsl:call-template>
		</xsl:variable>

		<!--
		We can't simply use msxsl:node-set($resourceDictionaries)/value since it
		doesn't work due to the "custom" default namespace of this transformation.
		-->
		<xsl:for-each select="msxsl:node-set($resourceDictionaries)/*[name() = 'value']">
			<xsl:variable name="resourceDictionary" select="."/>

			<ResourceDictionary>
				<xsl:attribute name="Source">
					<xsl:value-of select="normalize-space($resourceDictionary)"/>
				</xsl:attribute>
			</ResourceDictionary>
		</xsl:for-each>
	</xsl:template>

	<!--Template for removing redundant line breaks-->
	<xsl:template match="*/text()[not(normalize-space())]" />

</xsl:transform>